import {
    fmt
    runtime
}

def Main(start any) (stop any) {
	print For<int>{Next2Lines}
	wait Wait
	---
	:start -> 99..-1 -> print -> wait -> :stop
}

def Next2Lines(data int) (sig any) {
	first Tap<int>{FirstLine}
	dec Dec<int>
	second SecondLine
	---
	:data -> first -> dec -> second -> :sig
}

def FirstLine(data int) (sig any) {
	p1 fmt.Println
	p2 fmt.Println
	p3 fmt.Printf
	panic runtime.Panic
	---
	:data -> switch {
		0 -> 'No more bottles of beer on the wall, no more bottles of beer.' -> p1
		1 -> '1 bottle of beer on the wall, 1 bottle of beer.' -> p2
		_ -> [
			p3:args[0],
			'$0 bottles of beer on the wall, $0 bottles of beer.\n' -> p3:tpl
		]
	}
	[p1, p2, p3:sig] -> :sig
	p3:err -> panic
}

def SecondLine(data int) (sig any) {
	p1 fmt.Println
	p2 fmt.Println
	p3 fmt.Println
	p4 fmt.Printf
	panic runtime.Panic
	---
	:data -> switch {
		-1 -> 'Go to the store and buy some more, 99 bottles of beer on the wall.' -> p1
		0 -> 'Take one down and pass it around, no more bottles of beer on the wall.\n' -> p2 
		1 -> 'Take one down and pass it around, 1 bottle of beer on the wall.\n' -> p3
		_ -> [
			p4:args[0],
			'Take one down and pass it around, $0 bottles of beer on the wall.\n\n' -> p4:tpl
		]
	}
	[p1, p2, p3, p4:sig] -> :sig
	p4:err -> panic
}
